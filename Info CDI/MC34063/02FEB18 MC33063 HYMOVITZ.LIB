*==========================================================
* MC34063
* ON SEMICONDUTOR
* 1.5 A, Step-Up/Down/Inverting Switching Regulator
*
* This model was developed for On Semiconductor by:
* AEI Systems, LLC
* 5777 W. Century Blvd. Suite 876
* Los Angeles, California 90045
* Copyright 2002, all rights reserved.
*
* This model is subject to change without notice.
* Users may not directly or indirectly re-sell or 
* re-distribute this model. This model may not 
* be used, modified, or altered 
* without the consent of On Semiconductor. 
*
* For more information regarding modeling services,
* model libraries and simulation products, please
* call AEi Systems at (310) 863-8034, or contact 
* AEi by email: info@aeng.com. http://www.AENG.com
*
* Revision: 2/18/02, version 1.1
*==========================================================
**********
*SRC=MC33063;MC33063;Regulators;On Semiconductor;1.5A
*SYM=MC34063
.SUBCKT MC33063 swc    swe    ct    90    2    vdd    isns    drc
*               SW-col SW-em  Ct    gnd    cinv vdd    isns    drive col
*DC-DC controller
B5 5 0 V=~(v(9)&v(8))
Q1 ct isns vdd QN2907
.MODEL QN2907 PNP BF=200 BR=6 CJC=19PF CJE=23PF IKF=100E-3
+ IS=1.1E-12 ISE=1.3E-11 MJC=.2 MJE=1.25 NE=1.9 NF=1.21 RC=.6
+ TF=5E-10 TR=34E-9 VAF=50 VJC=.5 VJE=.85 XTB=1.5
B6 7 0 V=~(v(4)&v(10))
R3 5 10 100
R9 13 swe 100
C2 10 0 100p IC=5
R4 2 90 10MEG
R5 7 8 100
C3 8 0 100p IC=0
S1 srst 90 ct 90 _S2_mod  
.MODEL _S2_mod SW VT=1.75 VH=1.25
R1 srst vdd 10k
Q2 drc 14 13 _Q3_mod 
.MODEL _Q3_mod NPN BF=50
B4 6 0 V=v(2,90)  > (v(vref,90) + v(voff,90))  ? 0 : v(vdd)
B2 vdd ct I=V(srst,90) > 3 ? 35U : -220U
B3 9 0 V=(v(6,90) > 3) ? v(diff,90) > 1 ? 0 : v(vdd)
B7 16 90 V=V(vdd,90)-1.5 > 1.25 ? 1.25 : V(vdd,90)-1.25 < 0 ? 0 :V(vdd,90)-1.25
V7 16 vref
R6 vref 90 400
R7 vref vdd 90k
Q1x swc 13 swe _Q4_mod 
.MODEL _Q4_mod NPN BF=50 RC=.25 RE=.25 TF=0
R8 diff 90 10k
D1 14 15 DN4148
.MODEL DN4148 D BV=100V CJO=4PF IS=7E-09 M=.45 N=2 RS=.8
+ TT=6E-09 VJ=.6V
D2 swe 14 DN4148
V3 drc 15 DC=700m
B8 swe 14 I=v(5) > 2.5 ? 1m : -1m
V4 voff 90 DC=2m
C5 srst diff 10p
B1 4 90 V=(v(6,90) > 3) ? (v(diff,90) > -1) ? v(vdd) : 0
R10 vdd isns 10k
.ENDS
**********
